buildscript {
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath("com.android.tools.build:gradle:7.4.2") 
        classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:1.6.10") 
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
    }
}

def isNewArchitectureEnabled() {
  return rootProject.hasProperty("newArchEnabled") && rootProject.getProperty("newArchEnabled") == "true"
}

apply plugin: "com.android.library"
apply plugin: "kotlin-android"

if (isNewArchitectureEnabled()) {
  apply plugin: "com.facebook.react"
}

def supportsNamespace() {
  def parsed = com.android.Version.ANDROID_GRADLE_PLUGIN_VERSION.tokenize('.')
  def major = parsed[0].toInteger()
  def minor = parsed[1].toInteger()

  // Namespace support was added in 7.3.0
  return (major == 7 && minor >= 3) || major >= 8
}

android {
  if (supportsNamespace()) {
    namespace "com.reactnativevectoricons.rnvector"

    sourceSets {
      main {
        manifest.srcFile "src/main/AndroidManifestNew.xml"
      }
    }
  }

  compileSdkVersion 31
}

dependencies {
  implementation "com.facebook.react:react-native:+"
}

if (isNewArchitectureEnabled()) {
  react {
    jsRootDir = file("../src/")
    libraryName = "VectorIconsRnVector"
    codegenJavaPackageName = "com.reactnativevectoricons.rnvector"
  }
}

android.sourceSets.main.assets.srcDirs += file("$buildDir/intermediates/RNVI-rnvector")

task copyFonts(type: Copy) {
  from "../fonts"
  include "*.ttf"

  into "${buildDir}/intermediates/RNVI-rnvector/fonts"
  eachFile { println "(RNVI:rnvector) Copying font ${it.file}" }
}

preBuild.dependsOn(copyFonts)
